' Gambas class file

' Innova Project - Launcher of APP
'
' Copyright (C) Herberth Guzmán
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 2 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA

' Homepage: http://http://sourceforge.net/projects/innovadesktop

Private Const NSTEP As Integer = 100

Private $hWelcome As CWelcome

Static Public Sub Run()
  
  Dim hForm As F_about_dist
  
  hForm = New F_about_dist
  hForm.ShowModal
  
End

Public Sub _new()
  
  Dim hImage As Image
  Dim hBackground As Image
  
  hImage = Image.Load("img/tawhid.png")
  hBackground = New Image(hImage.W, hImage.H, Color.SetAlpha(Color.TextBackground, 160))
  hBackground.PaintImage(hImage, 0, 0)
  Me.Picture = hBackground.Picture
  
End

Public Sub Form_Open()
  
  Dim hImage As Image
  Dim Y As Integer
  Dim E As Float
  
  Me.Background = &HFFFFFFFF&
  
  $hWelcome = New CWelcome(dwgWelcome)
  
  'txtAuthor.Background = svwAuthor.Background
  
  GetAuthor
  
  'InitUser
  
  '$hBack = New Picture(dwgAbout.W, dwgAbout.H)
  '$hBack.Fill(Color.White)
  
  hImage = New Image(picGradientTop.W, picGradientTop.H)
  For Y = 0 To hImage.H - 1
    E = (Y / hImage.H)
    hImage.FillRect(0, Y, hImage.W, 1, Color.SetAlpha(Color.TextBackground, E * 255))
  Next
  picGradientTop.Picture = hImage.Picture
  
  For Y = 0 To hImage.H - 1
    E = 1 - (Y / hImage.H)
    hImage.FillRect(0, Y, hImage.W, 1, Color.SetAlpha(Color.TextBackground, E * 255))
  Next
  picGradientBottom.Picture = hImage.Stretch(hImage.W, picGradientBottom.H).Picture
  
  'lblMessage.Text = "© 2000-" & Year(Now) & " Benoît Minisini"
  Me.Center
  
End

Public Sub btnClose_Click()
  
  Me.Close
  
End

Private Sub RemoveLink(sStr As String) As String
  
  Dim iPos As Integer
  Dim iPos2 As Integer
  
  Do
    
    iPos = InStr(sStr, "<a href=")
    If iPos = 0 Then Break
    
    iPos2 = InStr(sStr, ">", iPos)
    If iPos2 = 0 Then iPos2 = Len(sStr) + 1
    sStr = Left(sStr, iPos - 1) & Mid(sStr, iPos2 + 1)
    
  Loop
  
  Return Replace(sStr, "</a>", "")
  
End

Private Function GetStars(iStar As Integer) As String
  
  Dim sStar As String
  Dim iInd As Integer
  
  For iInd = 1 To iStar
    If ((iInd - 1) Mod 5) = 0 Then
      sStar &= "★"
    Else
      sStar &= "☆"
    Endif
  Next
  
  Return sStar  
  
End

Private Sub GetAuthor()
  
  Dim hFic As File
  Dim sText As String
  Dim sLig As String
  Dim sMail As String
  Dim sCountry As String
  Dim sName As String
  Dim aAuthor As New String[]
  Dim sAuthor As String
  Dim aSplit As String[]
  
  'hFic = Open "authors.txt"
  hFic = Open "authors_dist.txt"
  
  While Not Eof(hFic)
    
    Line Input #hFic, sLig
    If Not sLig Then Continue
    If Left(sLig) = "-" Then Continue
    
    sName = sLig
    Line Input #hFic, sMail
    Line Input #hFic, sCountry
    
    sText = ""
    While Not Eof(hFic)
      
      Line Input #hFic, sLig
      If Not sLig Then Continue
      If Left(sLig) = "-" Then Break
      
      sLig = RemoveLink(sLig)
      If Right(sLig) <> "." Then sLig &= "."
      sText &= sLig & "<br>"
      
    Wend
    
    If Right(sText, 4) = "<br>" Then sText = Left(sText, -4)
    
    aAuthor.Add(sName & "\n\n" & sMail & "\n" & sCountry & "\n" & sText)
    
  Wend
  
  Close #hFic
  
  ' aAuthor.Sort(gb.Language + gb.IgnoreCase)
  
  sText = ""
  
  For Each sAuthor In aAuthor
    
    aSplit = Split(sAuthor, "\n")
    
    sName = aSplit[0]
    
    If aSplit.Count = 2 Then
      
      sText &= "<font size=+1>" & aSplit[0] & "</font>"
      sText &= "<blockquote>"
      sText &= ("Financial support") & "<br>" & GetStars(CInt(aSplit[1]))
      
    Else
      
      sText &= "<b><font size=+1>" & aSplit[0] & "</font>"
      If aSplit[2] Then sText &= " (" & aSplit[2] & ")"
      sText &= "<blockquote>"
      If aSplit[3] Then sText &= "<u>" & aSplit[3] & "</u><br>"
      sText &= aSplit[4]
      If aSplit[1] Then sText &= "<br>" & ("Financial support") & "<br>" & GetStars(CInt(aSplit[1]))
      sText &= "</b>"
      
    Endif
    
    sText &= "</blockquote><br>\n"
    
  Next
  
  'txtAuthor.X = Desktop.Scale
  'txtAuthor.W = txtAuthor.Parent.W - Desktop.Scale * 2
  txtAuthor.Text = sText
  txtAuthor.Adjust
  'txtAuthor.Height += 128
  'txtAuthor.W = txtAuthor.Parent.W - Desktop.Scale * 2
  
Catch
  
  txtAuthor.Text = Error.Text
  txtAuthor.Adjust
  
End

Public Sub timAuthor_Timer()
  
  Dim Y As Integer
  
  Y = svwAuthor.ScrollY
  svwAuthor.ScrollY += 1
  If Y = svwAuthor.ScrollY Then
    svwAuthor.ScrollY = 0
  Endif
  
End

Public Sub Form_KeyPress()
  
  If Key.Code = Key.Escape Then Me.Close
  
End

Public Sub dwgWelcome_MouseDown()
  
  Me.Close
  
End

Public Sub svwAuthor_MouseDown()
  
End

Public Sub dwgWelcome_Draw()
  
End

Public Sub Panel2_MouseDown()
  
End
